load("union-find-2-dot.stlx");
// This class implements the union find algorithm.
unionFind := procedure(M, R) {
    Parent := { [x, x] : x in M };
    Height := { [x, 1] : x in M };
    count  := 1;
    for ([x, y] in R) {
        graph2Dot(Parent, "union-find-example-$count$");
        count += 1;
        rootX := find(x, Parent);
        rootY := find(y, Parent);
        if (rootX != rootY) {
            if (Height[rootX] < Height[rootY]) {
                Parent[rootX] := rootY;  
            } else if (Height[rootX] > Height[rootY]) {
                Parent[rootY] := rootX;  
            } else {
                Parent[rootY] := rootX;  
                Height[rootX] += 1;
            }
        }
    }
    graph2Dot(Parent, "union-find-example");
    Roots := { x : x in M | Parent[x] == x };
    return { { y : y in M | find(y, Parent) == root } : root in Roots };
};
find := procedure(x, Parent) {
    if (Parent[x] == x) {
        return x;
    }
    return find(Parent[x], Parent);
};

demo := procedure() {
    M := { 1 .. 12 };
    R := [ [1, 2], [3, 4], [5, 6], [7, 8], [9, 10], [11, 12], [2, 4], [6, 8], [10, 12], [2, 8], [12, 2] ];
    print("R = $R$");
    P := unionFind(M, R);
    print("P = $P$");
};

demo();

