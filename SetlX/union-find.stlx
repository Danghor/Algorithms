// This class implements the union find algorithm.
unionFind := procedure(M, R) {
    Parent := { [x, x] : x in M };
    Height := { [x, 1] : x in M };
    for ([x, y] in R) {
        rootX := find(x, Parent);
        rootY := find(y, Parent);
        if (rootX != rootY) {
            if (Height[rootX] < Height[rootY]) {
                Parent[rootX] := rootY;  
            } else if (Height[rootX] > Height[rootY]) {
                Parent[rootY] := rootX;  
            } else {
                Parent[rootY] := rootX;  
                Height[rootX] += 1;
            }
        }
    }
    Roots := { x : x in M | Parent[x] == x };
    return { { y : y in M | find(y, Parent) == root } : root in Roots };
};
find := procedure(x, Parent) {
    if (Parent[x] == x) {
        return x;
    }
    return find(Parent[x], Parent);
};

demo := procedure() {
    M := { 1 .. 9 };
    R := { [1, 4], [7, 9], [3, 5], [2, 6], [5, 8], [1, 9], [4, 7] };
    print("R = $R$");
    P := unionFind(M, R);
    print("P = $P$");
};

demo();

