class Graph;
    procedure create(Distance, xCoord, yCoord);
    procedure shortest_path(source);
end Graph;

class body Graph;
    -- We use Tk for visualization.
    use Tk;

    -- Edges is a binary relation representing a function. If "id" is a node
    -- identifier, then edge(id) is a set consisting of all nodes that can
    -- be reached from the node given as argument.
    var mEdges;
    -- mEdgePairs contains the set of all edges [v, w] such that there is an edge 
    -- from v to w.
    var mEdgePairs;

    -- Given to node identifiers "a" and "b", Distance(a, b) computes the 
    -- length of the edge leading from a to b.
    var mDistance;

    ------------------------------------------------------------------------
    -- The variables below are only used for visualization.
    ------------------------------------------------------------------------

    -- Given a node identifier "id", xCoord(id) gives the x-coordinate of
    -- the node, while yCoord(id) yields the y-coordinate.
    var mXCoord;
    var mYCoord;

    -- TkIP is the Tk-interpreter
    var mTkIP;

    -- specfies How long to wait between redisplays
    var mDelay;

    var mChanged := {};

    -- if set to true, wait
    var mPause;
    var mStep;
    var mStepLoop := true;
    var mInitial  := true;

    -- This constructor gets three arguments: Distance, xCoord, yCoord, TkInterpr
    -- 1. Distance is a set of pair of the form [ [ id1, id2 ], d ], where
    --    (a) id1 and id2 are node identifiers
    --    (b) d ist the distance between node id1 and node id2.
    --    Since Distance is a binary relation, it is used as a function.
    -- 2. xCoord is a binary relation giving the x-coordinates of the nodes.
    -- 3. yCoord is a binary relation giving the x-coordinates of the nodes.
    -- The last two arguments are only used for the visualization.
    procedure create(Distance, xCoord, yCoord);
        mDistance := Distance;
        mXCoord   := xCoord;
        mYCoord   := yCoord;
        mEdges    := {};
        for [ pair, d ] in mDistance loop
            [ a, b ] := pair;
            if mEdges(a) = om then
                mEdges(a) := { b };
            else
                mEdges(a) +:= { b };
            end if;
        end loop;
        mEdgePairs := domain(Distance);

        -- The rest is for visualization
        mTkIP := tk_create();

	tk_call(mTkIP, "font create niceFont -size 18 -family chevara");
	tk_call(mTkIP, "font create otherFont -size 14 -family arioso");
	tk_call(mTkIP, "font create BigFont -size 20");
	tk_call(mTkIP, "wm title . \"Diskstra's Shortest Path Algorithm\"");
	tk_call(mTkIP, "frame .upper -bg gold" );
	tk_call(mTkIP, "scale .upper.slider -bg gold -label \"Time delay\" -showvalue true -length 800 -troughcolor pink " +
                      "-orient horizontal -from 0 -to 100 -tickinterval 5 -variable delay");
        tk_call(mTkIP, "pack .upper.slider -side left  -fill x -expand 1");
        
        tk_call(mTkIP, "frame .upper.right -bg gold");
        tk_call(mTkIP, "button .upper.right.run -text Run -font BigFont -command run -fg black -bg gold");
        tk_call(mTkIP, "pack .upper.right.run -side top -fill both -expand 1");
        tk_call(mTkIP, "bind .upper.right.run <Return> run");
        tk_call(mTkIP, "button .upper.right.step -text Step -font BigFont -command step -fg black -bg gold");
        tk_call(mTkIP, "pack .upper.right.step -side top -fill both -expand 1");
        tk_call(mTkIP, "bind .upper.right.step <Return> step");

        tk_call(mTkIP, "pack .upper.right -side left  -fill both -expand 1");

        tk_call(mTkIP, "canvas .cv -width 1000 -height 700 -bg white");
        tk_call(mTkIP, "pack .upper .cv -fill both -expand 1");
	tk_call(mTkIP, ".cv create text 500 20 -text \"Naive Shortest Path Algorithm\"" +
                       " -font niceFont -fill blue");
	tk_call(mTkIP, ".cv create text 990 680 -text \"presented by Karl Stroetmann\"" +
                       " -font otherFont -fill #505 -anchor e");
        tk_createcommand(mTkIP, "run", start);
        tk_createcommand(mTkIP, "step", step);

        c := 0;
        Nodes := domain(mEdges);
        -- draw the nodes
        for id in Nodes loop
            radius := 4;
            x := mXCoord(id);
            y := mYCoord(id);
            tk_call(mTkIP, ".cv create oval " + 
                           (x - radius) + " " + (y - radius) + " " + 
                           (x + radius) + " " + (y + radius) + 
                           " -fill black -tag n" + id);
            -- tk_call(mTkIP, ".cv create text " + (x + 5) + " " + (y - 5) + " " + " -text " + id + " -anchor sw");
        end loop;
        -- draw the edges
        for id in Nodes loop
            for node in mEdges(id) loop
                if id < node then
                    tk_call(mTkIP, ".cv create line " + 
                           mXCoord(id) + " " + mYCoord(id) + " " + 
                           mXCoord(node) + " " + mYCoord(node) + 
                           " -fill black -tag e" + id + "_" + node );
                    d := mDistance(id, node);
                    x := (mXCoord(id) + mXCoord(node)) / 2;
                    y := (mYCoord(id) + mYCoord(node)) / 2;
                    if abs(mXCoord(id) - mXCoord(node)) > abs(mYCoord(id) - mYCoord(node)) then
                        tk_call(mTkIP, ".cv create text " + x + " " + (y - 10) + " -text " + d + " -fill brown");
                    else
                        tk_call(mTkIP, ".cv create text " + (x + 10) + " " + y + " -text " + d + " -fill brown");
                    end if;
                end if;
            end loop;
        end loop;
        tk_mainloop(mTkIP);
    end create;

    -- This procedure computes the shortest path from the node source to all nodes
    -- reachable from this node.  It implements a naive shortest path algorithm.
    procedure shortest_path(source);
        -- DistSource is a binary relation representing a function.  For all node
        -- identifiers "id" that have been visited, DistSource(id) is the distance
        -- of the shortest path from "source" to "id".
        DistSource := {};
        DistSource(source) := 0;
        -- visualize the source by drawing a circle around it
        circle(source);
        -- Run Phase: As long as the fringe is not empty, we pick that node
        -- from the fringe, that has the smallest distance to the source.
        -- Then we add all nodes that are adjacent to this node to the fringe.
        -- Furthermore, we have to take care of the possiblity that the distance 
        -- from the source of a node that is already on the fringe can decrease.

        while exists [v,w] in mEdgePairs | 
            DistSource(v) /= om and 
            (DistSource(w) = om or DistSource(v) + mDistance([v,w]) < DistSource(w))
        loop
            DistSource(w) := DistSource(v) + mDistance(v, w);
            showDistance(w, DistSource(w), v);
        end loop;
    end shortest_path;

    -- The procedures below are only needed for visualization.
    
    -- This procedure is bound to the "Run" button.  First, it starts the algorithm.
    -- Then, it toggles between running and waiting.
    procedure start();
        if mInitial = true then
            mInitial := false;
            mPause   := false;
            mStep     := false;
            mStepLoop := false;
            mDelay   := str2number(tk_call(mTkIP, "concat $delay"));
            tk_call(mTkIP, ".upper.right.run configure -bg gold -text Wait");
            shortest_path(16);
        elseif mPause = true or mStep then
            mStep     := false;
            mStepLoop := false;
            mDelay := str2number(tk_call(mTkIP, "concat $delay"));
            mPause := false;
            tk_call(mTkIP, ".upper.right.run configure -bg gold -text Wait");
        else
            mStep     := false;
            mStepLoop := false;
            mDelay := str2number(tk_call(mTkIP, "concat $delay"));
            mPause := true;
            tk_call(mTkIP, ".upper.right.run configure -bg magenta -text Run");
        end if;
    end start;

    procedure step();
        tk_call(mTkIP, ".upper.right.step flash");
        mPause := false;
        mStep  := true;
        if mInitial = true then
            mInitial := false;
            mDelay   := str2number(tk_call(mTkIP, "concat $delay"));
            shortest_path(16);
        elseif mStepLoop then
            mStepLoop := false;
        else 
            mStepLoop := true;
        end if;
        tk_call(mTkIP, ".upper.right.run configure -bg magenta -text Run");
    end step;

    -- Wait until the variable "mPause" gets false.
    procedure waitForPause();
        while true loop
            if not mPause then
                return;
            end if;
            tk_call(mTkIP, "after 1");
            tk_call(mTkIP, "update");
        end loop;
    end waitForPause;


    procedure showDistance(id, newDist, source);
        if mChanged(id) = om then
            mChanged(id) := 0;
        else 
            mChanged(id) +:= 1;
        end if;
        xId := mXCoord(id);
        yId := mYCoord(id);
        tk_call(mTkIP, ".cv itemconfigure t" + id + " -fill magenta" );
        tk_call(mTkIP, ".cv create text " + (xId - 5) + " " + (yId + 18 * mChanged(id)) + " " + 
                       " -text " + newDist + " -tag t" + id + " -fill red -anchor ne");
        blinkEdge(id, source);
    end showDistance;

    procedure blinkEdge(a, b);
        if b < a then
            blinkEdge(b, a);
            return;
        end if;
        tk_call(mTkIP, ".cv delete e" +  a + "_" + b);
        tag := "e" + a + ":" + b;
        xA  := mXCoord(a);
        yA  := mYCoord(a);
        xB  := mXCoord(b);
        yB  := mYCoord(b);
        tk_call(mTkIP, ".cv create line " + 
                      xA + " " + yA + " " + xB + " " + yB + 
                      " -fill blue -width 2 -tag " + tag );
        blink(tag, "red", "blue");
    end blinkEdge;

    -- Let a given "tag" blink 10 times by switching between the colors
    -- "oldColor" and "newColor".
    procedure blink(tag, oldColor, newColor);
        i := 1;
        while mStepLoop or i < 11 loop
            if not mStepLoop then
                waitForPause();
            end if;
            if mStepLoop then            
                delay := 100;
            else
                delay := mDelay;
            end if;
            tk_call(mTkIP, ".cv itemconfigure " + tag + " -fill " + oldColor);
            tk_call(mTkIP, "after " + delay);
            tk_call(mTkIP, "update");
            tk_call(mTkIP, ".cv itemconfigure " + tag + " -fill " + newColor);
            tk_call(mTkIP, "after " + delay);
            tk_call(mTkIP, "update");
            i +:= 1;
        end loop;
        if mStep then
            mStepLoop := true;
        end if;
    end blink;

    -- draw a circle around a node id 
    procedure circle(id);
        x := mXCoord(id);
        y := mYCoord(id);
        tk_call(mTkIP, ".cv create oval " + (x - 10) + " " + (y - 10) + 
                      " " + (x + 10) + " " + (y + 10) + " " +
                      " -width 2 -outline black");
        tk_call(mTkIP, ".cv itemconfigure n" + id + " -fill blue");
    end circle;

    procedure str2number(s);
	result := 0;
	for i in [1..#s] loop
	    result := 10 * result + abs(s(i)) - 48;
	end loop;
	return result;
    end str2number;

end Graph;

program main;
    use Graph;

    g := generateGraph(9, 6);

    procedure generateGraph(n, m);
        var xCoord := {};
        var yCoord := {};
        -- count is the number of nodes
        count := 0;
        -- length gives the average distance between nodes in x and y direction
        length := 100;
        -- Nodes is a list of all node identifiers.    
        Nodes  := [];
        Edges  := {};
        Distance := {};
        for i in [1 .. n] loop
            for j in [1 .. m] loop
                count +:= 1;
                Nodes +:= [ count ];
                Edges(count) := {};
                xCoord(count) := length * i + 30 * sin(10.1 * count);
                yCoord(count) := length * j + 30 * cos(10.1 * count);
                if i > 1 then
                    Edges(count) +:= { count - m };
                end if;
                if i < n then
                    Edges(count) +:= { count + m };
                end if;
                if j > 1 then
                    Edges(count) +:= { count - 1 };
                end if;
                if j < m then
                    Edges(count) +:= { count + 1 };
                end if;
            end loop;
        end loop;
        -- compute the distances
        for id in Nodes loop
            for node in Edges(id) loop
                d := pythagoras(id, node);
                Distance(id, node) := d;
                Distance(node, id) := d;
            end loop;
        end loop;
        -- add special hyper space connection form node 16 to node 30
        Distance +:= { [ [ 16, 30 ], 10 ] };

        -- generate the graph
        return Graph(Distance, xCoord, yCoord);

        -- compute the Euclidean distance between two nodes
        procedure pythagoras(a, b);
            xa := xCoord(a);
            ya := yCoord(a);
            xb := xCoord(b);
            yb := yCoord(b);
            result := floor(sqrt((xa - xb) ** 2 + (ya - yb) ** 2));
            return result;
        end pythagoras;

    end generateGraph;

end main;
