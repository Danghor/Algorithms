program main;
    -- We need some random numbers to test our sorting algorithm.
    use Random;

    -- Create a random number generator.
    RandomGen := Random(10, 58); 

    -- create a list with 1000 random integers
    list := [ RandomGen.nextRandom(): i in {1..20}]; 

    print(list);
    sorted := insertionSort(list);
    print(sorted);

    if list2Bag(list) = list2Bag(sorted) and isSorted(sorted) then
        print("Everything is fine.");
    else
        print("ERROR");
    end if;

    -- Convert a list L into a set of pairs. The first component of every pair is the
    -- element of L, the second number shows how often this element occurs in L.
    procedure list2Bag(L);
        R := { [x, 0] : x in L };
        for x in L loop    
            R(x) +:= 1;
        end loop;
        return R;
    end list2Bag;

    -- Check whether a list is sorted.
    procedure isSorted(L);
        if #L <= 1 then
            return true;
        end if;
        return L(1) <= L(2) and isSorted( L(2..) );
    end isSorted;

    -- Sort the given list using the insertion sort algorithm.
    procedure insertionSort(L);
        if L = [] then
            return L;
        end if;
        x := L(1);
        R := L(2..);
        return insert(x, insertionSort(R));
    end insertionSort;

    -- Insert the element x into the sorted list l.
    procedure insert(x, L);
        if L = [] then
            return [x];
        end if;
        y := L(1);
        R := L(2..);
        if x <= y then
            return [x] + L;
        else
            return [ y ] + insert(x, R);
        end if;
    end insert;

end main;
