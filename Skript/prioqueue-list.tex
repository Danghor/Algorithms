\subsection{Repräsentierung von Heaps durch Listen}
Anstatt  Heaps durch binäre Bäume zu repräsentieren, können wir Heaps auch
durch Listen darstellen.   Die Idee ist, einen Heap durch eine  Liste der Form \\[0.1cm]
\hspace*{1.3cm} 
$L = \bigl[ \pair(k_1,v_1), \pair(k_2,v_2), \pair(k_3,v_3), \cdots, \pair(k_n,v_n) \bigr]$
\\[0.1cm]
zu repräsentieren.  Dazu müssen wir zunächst festlegen, wie eine solche Liste als Heap
interpretiert werden kann.  Wir definieren daher eine Funktion \\[0.1cm]
\hspace*{1.3cm} 
$\textsl{interpret}: \textsl{List}\bigl((\textsl{Key}\times \textsl{Value})\cup \{\Omega\}\bigr) \times \mathbb{N}  \rightarrow \textsl{Heap}$
\\[0.1cm]
die eine Liste, deren Elemente entweder Paare von Prioritäten und Werten sind, oder aber
der spezielle Wert $\Omega$, in einem Heap umwandelt.  Die Funktion \textsl{interpret}
bekommt als zweites Argument einen Index, der in die Liste zeigt. Die Idee ist, dass der
Wert, der in der Liste unter diesem Index abgespeichert ist, die Wurzel des Baums
$\textsl{interpret}(L,i)$ markiert.  Bei der Definition von \textsl{interpret} gibt es zwei
Fälle:
\begin{enumerate}
\item $L(i) = \Omega$.  Dann gilt natürlich \ 
       $\textsl{interpret}(L,i) = \textsl{nil}$.
\item $L(i) = \pair(k, v)$. Dann gilt:\\[0.1cm]
      \hspace*{1.3cm} 
      $\textsl{interpret}(L,i) = \textsl{node}\bigl(k,v,\textsl{interpret}(L,2*i),\textsl{interpret}(L,2*i+1)\bigr)$.

      Die Idee bei dieser Definition ist es, dass der linke Sohn des durch den Index $i$
      repräsentierten Knotens an der Stelle $2*i$ abgespeichert wird, während der rechte
      Sohn unter dem Index $2*i + 1$ abgespeichert wird.
\end{enumerate}
Abbildung \ref{fig:heap-list} auf Seite \pageref{fig:heap-list} zeigt einen Heap, der die Zahlen $1, \cdots, 9$ als
Prioritäten enthält.  Als Werte sind die Buchstaben ``\texttt{a}'', $\cdots$,
``\texttt{i}'' eingetragen.
  Als Liste repräsentiert hat dieser Heap die Form \\[0.1cm]
\hspace*{1.3cm} $L = \bigl[ \pair(1,\mathtt{a}), \pair(2,\mathtt{b}),
\pair(3,\mathtt{c}), \pair(4,\mathtt{d}), \pair(6,\mathtt{f}), 
\pair(5,\mathtt{e}), \pair(7,\mathtt{g}), \pair(8,\mathtt{h}), \Omega, \Omega,
\Omega, \pair(9, \mathtt{i}) \bigr]$.
\\[0.1cm]
Diese Liste hat gewissermassen Löcher, die durch die drei $\Omega$s repräsentiert
werden. Diese $\Omega$s entsprechen dem rechten Teilbaum des mit $4$ beschrifteten Knotens
und die beiden Teilbäume des mit $6$ beschrifteten Knotens.
Wollen wir Heaps durch Listen auf die oben skizzierte Weise implementieren, so brauchen wir außer der Liste $L$ noch
eine Liste \textsl{Counts}, wobei $\textsl{Counts}(i)$ die Zahl der Knoten in dem Teilbaum
angibt, dessen Wurzel an der Stelle $i$ in der Liste $L$ steht. 
Schließlich benötigen wir zur effizienten Implementierung der Methode $\textsl{change}()$
noch eine Funktion, die zu einem gegebenen Wert $v$ den Index $i$ berechnet, für den 
 $L(i) = \pair(k,v)$ \\[0.1cm]
gilt.  Wir realisieren diese Funktion in Form einer binären Relation.  Diese binäre
Relation speichern wir in der Instanz-Variablen \texttt{nodeIndex} ab.
Das folgende Listing zeigt die Listen-basierte Implementierung der Klasse \textsl{Heap}.

\begin{Verbatim}[ frame         = lines, 
                  framesep      = 0.3cm, 
                  labelposition = bottomline,
                  numbers       = left,
                  numbersep     = -0.2cm,
                  xleftmargin   = 0.8cm,
                  xrightmargin  = 0.8cm
                ]
    class body Heap;
   
        var L;
        var Counts;
        var nodeIndex;
   
        procedure create();
            L      := [];
            Counts := [];
            nodeIndex := {};
        end create;
   
        procedure insert(k, v);
            insertIndex(k, v, 1);
        end insert;
   
        procedure insertIndex(k, v, idx);
            if L(idx) = om then
                L(idx) := [ k, v ];
                Counts(idx) := 1;
                Counts(2 * idx) := 0;
                Counts(2 * idx + 1) := 0;
                nodeIndex(v) := idx;
                return;
            end if;
            Counts(idx) +:= 1;
            [ k1, v1 ] := L(idx);
            if k1 <= k then
                if Counts(2 * idx + 1) < Counts(2 * idx) then
                    insertIndex(k, v, 2 * idx + 1);
                else 
                    insertIndex(k, v, 2 * idx);
                end if;
                return;
            end if;
            L(idx) := [k, v];
            nodeIndex(v)  := idx;
            nodeIndex(v1) := om;
            if Counts(2 * idx + 1) < Counts(2 * idx) then
                insertIndex(k1, v1, 2 * idx + 1);
            else
                insertIndex(k1, v1, 2 * idx);
            end if;
        end insertIndex;
   
        procedure change(v, k);
            idx := nodeIndex(v);
            if idx = 1 then
                L(1) := [k, v];
            else
                parentIdx := idx / 2;
                [ kp, vp ] := L(parentIdx);
                if k < kp then
                    L(idx)       := [ kp, vp ];
                    L(parentIdx) := [ k, v ];
                    nodeIndex(vp) := idx;
                    nodeIndex(v)  := parentIdx;
                    change(v, k);
                else 
                    L(idx) := [ k, v ];
                end if;
            end if;
        end change;
   
        procedure top();
            return L(1);
        end top;
   
        procedure remove();
            removeIndex(1);
        end remove;
   
        procedure removeIndex(idx);
            if L(idx) = om then
                return;
            end if;
            Counts(idx) -:= 1;
            [k, v] := L(idx);
            nodeIndex(v) := om;
            if L(2 * idx) = om and L(2 * idx + 1) = om then
                L(idx) := om;
                Counts(2 * idx) := om;
                Counts(2 * idx + 1) := om;
                return;
            end if;
            if L(2 * idx) /= om and L(2 * idx + 1) = om then
                [k1, v1] := L(2 * idx);
                L(idx) := L(2 * idx);
                removeIndex(2 * idx);
                nodeIndex(v1) := idx;
                return;
            end if;
            if L(2 * idx) = om and L(2 * idx + 1) /= om then
                [k2, v2] := L(2 * idx + 1);
                L(idx) := L(2 * idx + 1);
                removeIndex(2 * idx + 1);
                nodeIndex(v2) := idx;
                return;
            end if;
            [ k1, v1 ] := L(2 * idx);
            [ k2, v2 ] := L(2 * idx + 1);
            if k1 <= k2 then
                L(idx) := [ k1, v1 ];
                removeIndex(2 * idx);
                nodeIndex(v1) := idx;
            else
                L(idx) := [ k2, v2 ];
                removeIndex(2 * idx + 1);
                nodeIndex(v2) := idx;
            end if;
        end removeIndex;
   
        procedure isEmpty();
            return L(1) = om;
        end isEmpty;
   
    end Heap;
\end{Verbatim}

%%% Local Variables: 
%%% mode: latex
%%% TeX-master: "algorithmen"
%%% End: 
