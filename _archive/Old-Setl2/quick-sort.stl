program main;
    -- We need some random numbers to test our sirtung algorithm.
    use Random;

    -- Create a random number generator.  The second value is used as a seed.
    RandomGen := Random(100, 58); 

    -- create a list with 1000 random integers
    list := [ RandomGen.nextRandom(): i in {1..38}]; 

    print(list);
    sorted := quickSort(list);
    print(sorted);

    if list2Bag(list) = list2Bag(sorted) and isSorted(sorted) then
        print("Everything is fine.");
    else
        print("ERROR");
    end if;

    -- Convert a list L into a set of pairs. The first component of every pair is the
    -- element of L, the second number shows how often this element occurs in L.
    procedure list2Bag(L);
        R := { [x, 0] : x in L };
        for x in L loop    
            R(x) +:= 1;
        end loop;
        return R;
    end list2Bag;

    -- Check whether a list is sorted.
    procedure isSorted(L);
        if #L < 2 then
            return true;
        end if;
        return L(1) <= L(2) and isSorted( L(2..) );
    end isSorted;

    procedure quickSort(L);
        if #L < 2 then
            return L;
        end if;
        x := L(1); R := L(2..);
        [ L1, L2 ] := partition(x, R);
        return quickSort(L1) + [x] + quickSort(L2);
    end quickSort;

    procedure partition(x, L);
        if L = [] then
            return [ [], [] ];
        end if;
        y := L(1); R := L(2..);
        [ R1, R2 ] := partition(x, R);
        if y <= x then
            return [ [y] + R1, R2 ];
        else
            return [ R1, [y] + R2 ];
        end if;
    end partition;
end main;
